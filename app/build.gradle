import org.aspectj.bridge.MessageHandler
import org.aspectj.tools.ajc.Main

apply plugin: 'com.android.application'
apply plugin: 'io.objectbox'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {

    signingConfigs {
        debug {
            storeFile file("${rootDir}/debug.keystore")
        }
    }

    compileSdkVersion 27

    defaultConfig {
        //app package name
        applicationId "com.ddu"

        minSdkVersion 19
        targetSdkVersion 27

        versionCode 1
        versionName "1.0"

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }
        multiDexEnabled = true
    }
    buildTypes {
        debug {
            //debug模式
            manifestPlaceholders = [BUILD_TYPE_VALUE: "test"]
            signingConfig signingConfigs.debug
            versionNameSuffix "(debug)"
//            applicationIdSuffix ".debug"
        }
        release {
            //是否进行混淆
            minifyEnabled false
            //Zipalign优化
            zipAlignEnabled true
            //混淆文件位置
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            manifestPlaceholders = [BUILD_TYPE_VALUE: "release"]
        }

    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def buildName = variant.buildType.name
            println("buildName: ${buildName}")
            def formattedDate = new Date().format('yyyy.MM.dd.HH.mm')
            outputFileName = "app-${buildName}-${defaultConfig.versionName}-${formattedDate}.apk"
            if (buildName.equalsIgnoreCase("debug")) {
                def path = output.outputFile.absolutePath
                variant.assemble.doLast {
                    println(path)
                }
            }
        }
    }

    //移除lint检查的error
    lintOptions {
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    productFlavors {
    }

    dataBinding {
        enabled = true
    }

//    configurations.all {
//        resolutionStrategy.force "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
//        resolutionStrategy.force "com.android.support:support-compat:$rootProject.supportLibraryVersion"
//        resolutionStrategy.force "com.android.support:support-core-utils:$rootProject.supportLibraryVersion"
//    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':icore')
    implementation project(':iannotation')
    kapt project(':iprocessors')

    implementation "com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar"

    implementation "com.facebook.rebound:rebound:0.3.8"

    implementation "org.quanqi:mpandroidchart:1.7.5"

    implementation "com.google.android:flexbox:0.3.2"

    implementation "com.google.zxing:core:3.3.3"
    implementation "com.google.zxing:android-core:3.3.0"

    implementation "io.objectbox:objectbox-android:2.2.0"
    implementation "io.objectbox:objectbox-kotlin:2.2.0"
    kapt  "io.objectbox:objectbox-processor:2.2.0"
    annotationProcessor "io.objectbox:objectbox-processor:2.2.0"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:1.6.1"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:1.6.1"

    implementation "com.github.bumptech.glide:glide:4.8.0"
    implementation "com.github.bumptech.glide:compiler:4.8.0"
    kapt "com.github.bumptech.glide:compiler:4.8.0"

    implementation "com.squareup.okhttp3:okhttp:3.11.0"
    implementation "com.squareup.okhttp3:logging-interceptor:3.11.0"
    implementation "com.squareup.okio:okio:2.0.0"

    implementation "org.aspectj:aspectjrt:1.9.1"

    implementation "com.android.support:design:27.1.0"
    implementation "com.android.support:recyclerview-v7:27.1.0"
    implementation "com.android.support:cardview-v7:27.1.0"
    implementation "com.android.support:palette-v7:27.1.0"

    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'org.ligboy.retrofit2:converter-fastjson-android:2.1.0'

    implementation 'in.srain.cube:ultra-ptr:1.0.11'

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.orhanobut:logger:2.1.1'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.5'

    implementation 'com.jakewharton.rxbinding2:rxbinding-kotlin:2.1.1'

    implementation 'pub.devrel:easypermissions:1.2.0'
    implementation "org.jetbrains.anko:anko:0.10.5"
//    kapt "com.android.databinding:compiler:$Versions.gradle_version"

}

final def variants = project.android.applicationVariants
//在构建工程时，执行编织
variants.all { variant ->
    if (!variant.buildType.isDebuggable()) {
        return
    }
    JavaCompile javaCompile = variant.javaCompile
    javaCompile.doLast {
        String[] args = ["-showWeaveInfo",
                         "-1.7",
                         "-inpath", javaCompile.destinationDir.toString(),
                         "-aspectpath", javaCompile.classpath.asPath,
                         "-d", javaCompile.destinationDir.toString(),
                         "-classpath", javaCompile.classpath.asPath,
                         "-bootclasspath", project.android.bootClasspath.join(File.pathSeparator)]

        MessageHandler handler = new MessageHandler(true)
        new Main().run(args, handler)
    }
}