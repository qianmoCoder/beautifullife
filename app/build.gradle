apply plugin: 'com.android.application'
apply plugin: 'com.aliyun.ams.emas-services'
apply plugin: 'io.objectbox'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {

    Properties properties = new Properties()
    properties.load(project.file('./local.properties').newDataInputStream())

    signingConfigs {
        debug {
            storeFile file("${rootDir}/debug.keystore")
        }
        release {
            storeFile file(properties.getProperty("keystroe_storeFile"))
            storePassword properties.getProperty("keystroe_storePassword")
            keyAlias properties.getProperty("keystroe_keyAlias")
            keyPassword properties.getProperty("keystroe_keyPassword")
        }
    }

    compileSdkVersion Versions.compileSdkVersion

    defaultConfig {
        //app package name
        applicationId "com.ddu"

        minSdkVersion Versions.minSdkVersion
        targetSdkVersion Versions.targetSdkVersion

        versionCode 1
        versionName "1.0"

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }
        multiDexEnabled = true
    }
    buildTypes {
        debug {
            //debug模式
            manifestPlaceholders = [BUILD_TYPE_VALUE: "test"]
            signingConfig signingConfigs.debug
            versionNameSuffix "(debug)"
//            applicationIdSuffix ".debug"
        }
        release {
            //是否进行混淆
            minifyEnabled false
            //Zipalign优化
            zipAlignEnabled true
            //混淆文件位置
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [BUILD_TYPE_VALUE: "release"]
        }

    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def buildName = variant.buildType.name
            println("buildName: ${buildName}")
            def formattedDate = new Date().format('yyyy.MM.dd.HH.mm')
            outputFileName = "app-${buildName}-${defaultConfig.versionName}-${formattedDate}.apk"
        }
    }

    //移除lint检查的error
    lintOptions {
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    productFlavors {
    }

    dataBinding {
        enabled = true
    }

    sourceSets {
        main {
            jni.srcDirs = []
        }

        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

//    configurations.all {
//        resolutionStrategy.force "androidx.vectordrawable:vectordrawable:1.0.1"
//        resolutionStrategy.force "androidx.appcompat:appcompat:1.0.2"
//    }

//    // 1
//    configurations.all {
//        // 2
//        resolutionStrategy {
//            // 3 显示版本冲突
//            failOnVersionConflict()
//        }
//    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':icore')
    implementation project(':iannotation')
    kapt project(':iprocessors')

    implementation "com.facebook.rebound:rebound:0.3.8"

    implementation "com.google.zxing:core:3.3.3"
    implementation "com.google.zxing:android-core:3.3.0"

    implementation "io.objectbox:objectbox-android:${Versions.objectBox}"
    implementation "io.objectbox:objectbox-kotlin:${Versions.objectBox}"
    kapt  "io.objectbox:objectbox-processor:${Versions.objectBox}"
    annotationProcessor "io.objectbox:objectbox-processor:${Versions.objectBox}"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:1.6.1"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:1.6.1"

    implementation "com.squareup.okhttp3:okhttp:4.0.0"
    implementation "com.squareup.okhttp3:logging-interceptor:3.12.1"
    implementation "com.squareup.okio:okio:2.2.2"

    implementation "com.google.android.material:material:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.palette:palette:1.0.0"

    implementation 'com.squareup.retrofit2:retrofit:2.6.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'

    implementation Libs.constraintlayout
    implementation 'com.orhanobut:logger:2.2.0'

    implementation 'pub.devrel:easypermissions:3.0.0'

    implementation Libs.coroutinesCore
    implementation Libs.coroutinesAndroid

    implementation Libs.lifecycle

    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.0.5.1'
    implementation Libs.glide
    kapt Libs.glideCompiler

    implementation 'com.aliyun.ams:alicloud-android-hotfix:3.2.8'
}
